import org.gradle.internal.os.OperatingSystem

buildscript {
    project.ext.kotlinVersion = '1.3.30'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.30'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

repositories {
    mavenCentral()
}

def getGitRef() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Throwable e) {
        return "dev"
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    group 'me.wieku'

    version = new Date().format("yy.MMdd")

    if (System.getenv("BUILD_NUMBER") != null)
        version += ".${System.getenv("BUILD_NUMBER")}"
    else
        version += "-" + getGitRef()

    ext {
        lwjglVersion = "3.2.1"
        jomlVersion = "1.9.13"
        switch (OperatingSystem.current()) {
            case OperatingSystem.LINUX:
                lwjglNatives = "natives-linux"
                break
            case OperatingSystem.MAC_OS:
                lwjglNatives = "natives-macos"
                break
            case OperatingSystem.WINDOWS:
                lwjglNatives = "natives-windows"
                break
        }

    }
    
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}